// Author Paulo M. De Oliveira pm.deoliveiradejes@uniandes.edu.co March 15, 2018
! Kersting NEV Test system
! n=1
! Full NEV approach (considering tower footings)
clear
New circuit.SOURCE_1    bus1=n1.1  bus2=n1.4 basekV=7.1996 pu=1 angle=45  Z1=[0.0983474,0.2950422133]   phase=1
New Vsource.SOURCE_2 bus1=n1.2  bus2=n1.4 basekV=7.1996 pu=1 angle=-75 Z1=[0.0983474,0.2950422133]   phase=1
New Vsource.SOURCE_3 bus1=n1.3 bus2=n1.4 basekV=7.1996 pu=1 angle=165 Z1=[0.0983474,0.2950422133]  phase=1
set earthmodel=carson
! **** DEFINE WIRE DATA 
new wiredata.conductor Runits=mi Rac=0.306 GMRunits=ft GMRac=0.0244  Radunits=in Diam=0.721 
new wiredata.neutral   Runits=mi Rac=0.592 GMRunits=ft GMRac=0.00814 Radunits=in Diam=0.563 
! **** DEFINE LINE GEOMETRY 
new linegeometry.4wire nconds=4 nphases=3 reduce=no 
~ cond=1 wire=conductor units=ft x=-4   h=28 
~ cond=2 wire=conductor units=ft x=-1.5 h=28 
~ cond=3 wire=conductor units=ft x=3    h=28 
~ cond=4 wire=neutral   units=ft x=0    h=24 
! **** 12.47 KV LINE!
new line.line1 geometry=4wire length=6000 units=ft bus1=n1.1.2.3.4 bus2=n2.1.2.3.4 Rho=100
 !Loads
New Load.load1a.1 Phases=1  Bus1=n2.1.4   kVA=3000 pf=0.9  kV=12.47 conn=wye  vminpu=0.1 vmaxpu=1.9
New Load.load1b.2 Phases=1  Bus1=n2.2.4   kVA=3500 pf=0.95   kV=12.47  conn=wye  vminpu=0.1 vmaxpu=1.9
New Load.load1c.3 Phases=1  Bus1=n2.3.4   kVA=2500 pf=0.85  kV=12.47 conn=wye vminpu=0.1 vmaxpu=1.9
New Reactor.SourceGround Phases=1 Bus1=n1.4 Bus2=n1.0 R=.5 X=0
New Reactor.Load1Ground Phases=1 Bus1=n2.4 Bus2=n2.0 R=5 X=0
set voltagebases=[12.47] 
calcvoltagebases     ! **** let DSS compute voltage bases
solve
! Various reports ...
show voltages LN Nodes 
show currents resid=yes elements  ! this shows sum of phase currents
Show Powers kva Elements
!show Y
!show Yprim

